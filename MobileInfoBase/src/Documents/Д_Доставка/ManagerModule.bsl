#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт	
// Володин А.А. 
// Задача 1 ДЗ Механизмы печати 
// 16 октября 2024 г.
	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
// Конец вставки от 16 октября 2024 г.
КонецПроцедуры

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
// Володин А.А. 
// Задача 1 ДЗ Механизмы печати 
// 16 октября 2024 г.	
	// Анкета клиента
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АнкетаКлиента";
	КомандаПечати.Представление = НСтр("ru = 'Анкета клиента'");
	КомандаПечати.Порядок = 10;
	
	// Товарная накладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТранспортнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Транспортная накладная'");
	КомандаПечати.Порядок = 20;
// Конец вставки от 16 октября 2024 г. 
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
// Володин А.А. 
// Задача 1 ДЗ Механизмы печати 
// 16 октября 2024 г.
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АнкетаКлиента");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьАнкеты(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Анкета клиента'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.Д_Доставка.ПФ_MXL_АнкетаКлиента";
	КонецЕсли;
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ТранспортнаяНакладная");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьТоварнойНакладной(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Транспортная накладная'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.Д_Доставка.ПФ_MXL_ТранспортнаяНакладная";
	КонецЕсли;
// Конец вставки от 16 октября 2024 г.
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьАнкеты(МассивОбъектов, ОбъектыПечати)
// Володин А.А. 
// Задача 1 ДЗ Механизмы печати 
// 16 октября 2024 г.
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АнкетаКлиента";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Д_Доставка.ПФ_MXL_АнкетаКлиента");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиЗаголовокАнкетыКлиента(ДанныеДокументов, ТабличныйДокумент, Макет);
						
        // В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;	
		
	Возврат ТабличныйДокумент;
// Конец вставки от 16 октября 2024 г.
КонецФункции

Функция ПечатьТоварнойНакладной(МассивОбъектов, ОбъектыПечати)
// Володин А.А. 
// Задача 1 ДЗ Механизмы печати 
// 16 октября 2024 г.
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ТранспортнаяНакладная";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Д_Доставка.ПФ_MXL_ТранспортнаяНакладная");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиЗаголовокТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет);

		ВывестиТоварыТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		ВывестиПодвалТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет);
		
        // В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;	
		
	Возврат ТабличныйДокумент;
// Конец вставки от 16 октября 2024 г.
КонецФункции

Функция ПолучитьДанныеДокументов(МассивОбъектов)
// Володин А.А. 
// Задача 1 ДЗ Механизмы печати 
// 16 октября 2024 г.
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Д_Доставка.Ссылка КАК Ссылка,
	|	Д_Доставка.Номер КАК Номер,
	|	Д_Доставка.Дата,
	|	Д_Доставка.Организация,
	|	Д_Доставка.Контрагент,
	|	Д_Доставка.Договор,
	|	Д_Доставка.АдресДоставки,
	|	Д_Доставка.Основание,
	|	Д_Доставка.Ответственный,
	|	Д_Доставка.Комментарий,
	|	Д_Доставка.Товары.(
	|		НомерСтроки,
	|		Ссылка,
	|		Номенклатура,
	|		Количество)
	|ИЗ
	|	Документ.Д_Доставка КАК Д_Доставка
	|ГДЕ
	|	Д_Доставка.Ссылка В (&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Возврат Запрос.Выполнить().Выбрать();
// Конец вставки от 16 октября 2024 г.
КонецФункции

Процедура ВывестиЗаголовокАнкетыКлиента(ДанныеДокументов, ТабличныйДокумент, Макет)
// Володин А.А. 
// Задача 1 ДЗ Механизмы печати 
// 16 октября 2024 г.
	ОбластьАнкетаКлиента = Макет.ПолучитьОбласть("АнкетаКлиента");
	
	ДанныеПечати = Новый Структура;
		
	ШаблонЗаголовка = "Анкета о доставке №%1 от %2 г.";
	ТекстЗаголовка = СтрШаблон(ШаблонЗаголовка,
		ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер),
		Формат(ДанныеДокументов.Дата, "ДФ='dd.MM.yyyy'"));
	ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
	
	ДанныеПечати.Вставить("Контрагент", ДанныеДокументов.Контрагент);
	
	ОбластьАнкетаКлиента.Параметры.Заполнить(ДанныеПечати);
	
	СтрокаСсылки = ПолучитьНавигационнуюСсылку(ДанныеДокументов.Ссылка);
	ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(СтрокаСсылки, 1, 120);
	
	Если Не ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
		
		ТекстСообщения = НСтр("ru = 'Не удалось сформировать QR-код.
									|Технические подробности см. в журнале регистрации.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		
	Иначе
		
		КартинкаКода = Новый Картинка(ДанныеQRКода);
		ОбластьАнкетаКлиента.Рисунки.QRКод.Картинка = КартинкаКода;
		
	КонецЕсли;
		
	ТабличныйДокумент.Вывести(ОбластьАнкетаКлиента);
// Конец вставки от 16 октября 2024 г.
КонецПроцедуры

Процедура ВывестиЗаголовокТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет)
// Володин А.А. 
// Задача 1 ДЗ Механизмы печати 
// 16 октября 2024 г.
	ОбластьЗаголовокТранспортнойНакладной = Макет.ПолучитьОбласть("ЗаголовокТранспортнойНакладной");
	
	ДанныеПечати = Новый Структура;
		
	ШаблонЗаголовка = "Транспортная накладная №%1 от %2 г.";
	ТекстЗаголовка = СтрШаблон(ШаблонЗаголовка,
		ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер),
		Формат(ДанныеДокументов.Дата, "ДФ='dd.MM.yyyy'"));
	ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
	
	ДанныеПечати.Вставить("Грузоотправитель", ДанныеДокументов.Организация);
	ДанныеПечати.Вставить("Грузополучатель", ДанныеДокументов.Контрагент);
	
	ОбластьЗаголовокТранспортнойНакладной.Параметры.Заполнить(ДанныеПечати);
	
	СтрокаСсылки = ПолучитьНавигационнуюСсылку(ДанныеДокументов.Ссылка);
	ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(СтрокаСсылки, 1, 120);
	
	Если Не ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
		
		ТекстСообщения = НСтр("ru = 'Не удалось сформировать QR-код.
									|Технические подробности см. в журнале регистрации.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		
	Иначе
		
		КартинкаКода = Новый Картинка(ДанныеQRКода);
		ОбластьЗаголовокТранспортнойНакладной.Рисунки.QRКод.Картинка = КартинкаКода;
		
	КонецЕсли;
	
	ТабличныйДокумент.Вывести(ОбластьЗаголовокТранспортнойНакладной);
// Конец вставки от 16 октября 2024 г.
КонецПроцедуры

Процедура ВывестиТоварыТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет)
// Володин А.А. 
// Задача 1 ДЗ Механизмы печати 
// 16 октября 2024 г.
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
		
	ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ВыборкаТовары = ДанныеДокументов.Товары.Выбрать();
	Пока ВыборкаТовары.Следующий() Цикл
		ОбластьСтрока.Параметры.Заполнить(ВыборкаТовары);
		ТабличныйДокумент.Вывести(ОбластьСтрока);
	КонецЦикла;
// Конец вставки от 16 октября 2024 г.		
КонецПроцедуры

Процедура ВывестиПодвалТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет)
// Володин А.А. 
// Задача 1 ДЗ Механизмы печати 
// 16 октября 2024 г.
	ОбластьПодвалТранспортнойНакладной = Макет.ПолучитьОбласть("ПодвалТранспортнойНакладной");

	ТабличныйДокумент.Вывести(ОбластьПодвалТранспортнойНакладной);
// Конец вставки от 16 октября 2024 г.
КонецПроцедуры

#КонецОбласти

#КонецЕсли
